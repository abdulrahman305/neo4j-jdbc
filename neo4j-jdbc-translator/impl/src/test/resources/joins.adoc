= Using joins to map relationships

On the surface joins are relationships materialized in SQL (Foreign keys are not).
Sadly, it's not as straight forward to map.
There are several options and possibilities to handle things:

* When joining two tables on a column, taking the left hand table column, use its name as relationship type and treat it as outgoing from left to right
* When joining two tables with an intersection table (which you usually model in SQL for m:n relationships with attributes), use the name of that intersection table as relationship type

We implemented some variants thereof, however we don't claim their absolute usefulness in all cases

== 1:n joins

=== Natural joins

SQL `NATURAL` joins are the easiest way to denote relationship names without having todo any mapping.

[source,sql,id=nj1,name=naturalJoin]
----
SELECT p, m FROM Person p
NATURAL JOIN Movie m
----

A one-hope `NATURAL JOIN` will resolve to an anonymous, wildcard relationship:

[source,cypher,id=nj1_expected]
----
MATCH (p:Person)-->(m:Movie) RETURN p, m
----

`NATURAL` joins can be chained like this, the connecting join table does not need to exist:

[source,sql,id=nj2,name=naturalJoins,metaData=Movie:title|released]
----
SELECT p.name, r.role, m.* FROM Person p
NATURAL JOIN ACTED_IN r
NATURAL JOIN Movie m
----

It will be turned into a Neo4j relation:

[source,cypher,id=nj2_expected]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
RETURN p.name, r.role,
       elementId(m) AS m_element_id, m.title AS title, m.released AS released
----

=== Simple join

We configured the translator use the following table mapping:

* `people` mapped to label `People`
* `movies` mapped to label `Movie`

With that in place, we translate

[source,sql,id=r1_1,name=foreign_key_join_simple,table_mappings=people:Person;movies:Movie]
----
SELECT p.name, m.title
FROM people p
JOIN movies m ON m.id = p.directed
----

to

[source,cypher,id=r1_1_expected]
----
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
RETURN p.name, m.title
----

`DIRECTED` is the uppercase version of the join column in the left table (`p.directed`).

We can add a join column mapping in the form of `people.movie_id:DIRECTED` if we have different column names:

[source,sql,id=r1_1b,name=foreign_key_join_simple,table_mappings=people:Person;movies:Movie,join_column_mappings=people.movie_id:DIRECTED]
----
SELECT p.name, m.title
FROM people p
JOIN movies m ON m.id = p.movie_id
----

to

[source,cypher,id=r1_1b_expected]
----
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
RETURN p.name, m.title
----

=== Using the `ON` clause

We used backticks here for the table- and column names and no mapping.

[source,sql,id=r1_0,name=foreign_key_join]
----
SELECT p.name, m.title
FROM `Person` as p
JOIN `Movie` as m ON (m.id = p.`DIRECTED`)
----

The translation yields in the same result as before

[source,cypher,id=r1_0_expected]
----
MATCH (p:Person)-[:DIRECTED]->(m:Movie)
RETURN p.name, m.title
----

== m:n joins

An intersection table is a table that contains references to two other tables in the form of at least two columnsâ€”hopefully with foreign keys and therefor indexes defined on them.
This construct is usually required in the relational model to create an m:n relationship.
Sometimes the intersection table also spots additional columns.
Of course, such an auxiliary construct is not necessary in Neo4j.
We can model as many outgoing and incoming relationships from one label to another as we desire and they can also have properties.
We can hover use that construct for our translator

The following example uses a configured mapping as follows:

* `people` mapped to label `People`
* `movies` mapped to label `Movie`
* `movie_actors` mapped to `ACTED_IN`

[source,sql,id=r2_0,name=join_table_join,table_mappings=people:Person;movies:Movie;movie_actors:ACTED_IN]
----
SELECT p.name, m.title
FROM people p -- <.>
JOIN movie_actors r ON r.person_id = p.id -- <.>
JOIN movies m ON m.id = r.person_id -- <.>
----
<.> The driving table from which we map outgoing relationships
<.> An intersection table, that is used again in the next `JOIN` clause
<.> The final join clause

We don't do semantic analysis, the order of the joins matter and will lead to the following query:

[source,cypher,id=r2_0_expected]
----
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
RETURN p.name, m.title
----

Multiple joins will result in a chain of relationships:

[source,sql,id=r2_1,name=join_multiple_table_join,table_mappings=people:Person;movies:Movie;movie_actors:ACTED_IN;movie_directors:DIRECTED]
----
SELECT p.name AS actor, d.name AS director, m.title
FROM people p
 JOIN movie_actors r ON r.person_id = p.id
 JOIN movies m ON m.id = r.person_id
 JOIN movie_directors r2 ON r2.movie_id = m.id
 JOIN people d ON r2.person_id = d.id
----

as demonstrated by

[source,cypher,id=r2_1_expected]
----
MATCH (p:`Person`)-[r:`ACTED_IN`]->(m:`Movie`)<-[r2:`DIRECTED`]-(d:`Person`)
RETURN p.name AS actor, d.name AS director, m.title
----

Notice how the direction of the `DIRECTED` relationship is defined by the order of the join columns.
