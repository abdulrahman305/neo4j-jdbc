= Translation concepts

== Table names to labels

The most simple select statement to translate is a statement without the `FROM` clause, such as:

[source,sql,id=t1_1,name=no_driving_table]
----
SELECT 1
----

It is equivalent without loss to the following Cypher:

[source,cypher,id=t1_1_expected]
----
RETURN 1
----

`SELECT` statements without further `JOIN` clauses are pretty straight forward to translate.
The hardest challenge here is how to map the table name to labels:

* We parse the SQL Statement case-sensitive by default
* Table names will be mapped to node labels
* Table aliases will be used as identifiable symbolic names

[source,sql,id=t1_0,name=select_with_condition]
----
SELECT t.a, t.b
FROM My_Table -- <.>
  AS t -- <.>
WHERE t.a = 1
----
<.> Will be used as the label to match, as-is, i.e. `My_Table`
<.> The table alias will become the node-alias

The whole construct will be translated to

[source,cypher,id=t1_0_expected]
----
MATCH (t:My_Table)
WHERE t.a = 1
RETURN t.a, t.b
----

We recommend using table aliases, but the translations works without them as well:

[source,sql,id=t1_2]
----
SELECT my_table.* FROM My_Table
----

In that case we use the lower-cased table name as symbolic name:

[source,cypher,id=t1_2_expected]
----
MATCH (my_table:My_Table)
RETURN my_table
----

In the example above we used de-referenced the whole table with the `*`, which was translated into the name for the table.
If you use the asterisk without an alias, it will be used as is:

[source,sql,id=t1_3]
----
SELECT * FROM My_Table
----

So that it returns all the names as well as properties if any:

[source,cypher,id=t1_3_expected]
----
MATCH (my_table:My_Table)
RETURN *
----

== Comparing SQL with Cypher examples

Sources of the following examples are from https://neo4j.com/developer/cypher/guide-sql-to-cypher/[Comparing SQL with Cypher].

=== Find all Products

=== Select and Return Records

Easy in SQL, just select everything from the `products` table.

[source,sql,id=t2_0,name=select_and_return_records,table_mappings=products:Product]
----
SELECT p.*
FROM products as p
----

Similarly, in Cypher, you just *match* a simple pattern: all nodes with the *label* `:Product` and `RETURN` them.

[source,cypher,id=t2_0_expected]
----
MATCH (p:Product)
RETURN p
----

=== Field Access, Ordering and Paging

*More efficient is to return only a subset of attributes*, like `ProductName` and `UnitPrice`.
And while we're on it, let's also order by price and only return the 10 most expensive items.

[source,sql,id=t2_1,name=field_acces_ordering_paging,table_mappings=products:Product]
----
SELECT p.`productName`, p.`unitPrice`
FROM products as p
ORDER BY p.`unitPrice` DESC
LIMIT 10
----

You can copy and paste the changes from SQL to Cypher, it's thankfully unsurprising.
But remember that labels, relationship-types and property-names are *case sensitive* in Neo4j.

[source,cypher,id=t2_1_expected]
----
MATCH (p:Product)
RETURN p.productName, p.unitPrice ORDER BY p.unitPrice DESC LIMIT 10
----

Default order direction will be translated as is:

[source,sql,id=t2_2,name=order_by_default]
----
SELECT * FROM Movies m ORDER BY m.title
----

[source,cypher,id=t2_2_expected,parseCypher=false]
----
MATCH (m:Movies)
RETURN * ORDER BY m.title
----
